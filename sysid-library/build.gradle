plugins {
  id "cpp"
}

apply from: "${rootDir}/config.gradle"
nativeUtils.withCrossRoboRIO()

model {
  components {
    libsysid(NativeLibrarySpec) {
      targetPlatform nativeUtils.wpi.nativePlatforms.roborio
      if (!project.hasProperty("skipDesktopRobots"))
        targetPlatform nativeUtils.wpi.nativePlatforms.desktop

      sources.cpp {
        source {
          srcDirs "src/main/cpp"
          include "**/*.cpp"
        }
        exportedHeaders.srcDirs "src/main/include"
      }

      binaries.all {
        if (it instanceof SharedLibraryBinarySpec)
          it.buildable = false

        it.cppCompiler.define("PROJECT_ROOT_DIR", "$rootDir")

        if (project.hasProperty("integration"))
          it.cppCompiler.define("INTEGRATION")
      }

      nativeUtils.wpi.vendorDeps.nativeVendor.cpp(it)
      nativeUtils.useRequiredLibrary(it, 'wpilib_executable_shared')
    }
  }
}
